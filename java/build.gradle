group 'ianhblakley.goai'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    compile group: 'io.grpc', name: 'grpc-netty', version: '1.0.1'
    compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.0.1'
    compile group: 'io.grpc', name: 'grpc-stub', version: '1.0.1'
    compile 'com.google.guava:guava:20.0'
}

apply plugin: 'com.google.protobuf'
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.7.0'
    }
}

sourceSets {
    main {
        proto {
            srcDir '../proto/'
        }
    }
}

clean {
    delete "$projectDir/src/main/generated"
    delete "$projectDir/logs"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.1.0"
    }
    //noinspection GroovyAssignabilityCheck
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.0.1"
        }
    }
    generatedFilesBaseDir = "$projectDir/src/main/generated"
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

apply plugin: 'javafx-gradle-plugin'
jfx {
    // minimal requirement for jfxJar-task
    mainClass = 'ianhblakley.goai.gui.Main'
    jfxMainAppJarName = "goai-jfx.jar"
    additionalAppResources= 'src'
    bundleArguments = [
            licenseType: 'GPLv3',
            licenseFile: 'LICENSE'
    ]
    jvmArgs = ["-Dlog4j.configurationFile=src/log4j2.xml", "-ea"]
    // minimal requirement for jfxNative-task
    vendor = 'Ian Blakley'
}

apply plugin: 'application'
run{
    standardInput = System.in
}
mainClassName = 'ianhblakley.goai.Main'
applicationDefaultJvmArgs = ["-Dlog4j.configurationFile=src/log4j2.xml", "-ea",
//                             "-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5006"
]
